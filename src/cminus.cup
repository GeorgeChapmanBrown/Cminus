/*
Created by:
    Brandon Tan
    George Chapman-Brown

cminus.cup
- Parser component
*/

   
/* ----------------------Preliminary Declarations Section--------------------*/
   
/* Import the class java_cup.runtime.*  */
//import java_cup.runtime.*;
import absyn.*;
   
/* Parser code to change the way the parser reports errors (include
   line and column number of the error). */
parser code {:
    
    /* Change the method report_error so it will display the line and
       column of where the error occurred in the input as well as the
       reason for the error which is passed into the method in the
       String 'message'. */
    public void report_error(String message, Object info) {
   
        /* Create a StringBuffer called 'm' with the string 'Error' in it. */
        StringBuffer m = new StringBuffer("Error");
   
        /* Check if the information passed to the method is the same
           type as the type java_cup.runtime.Symbol. */
        if (info instanceof java_cup.runtime.Symbol) {
            /* Declare a java_cup.runtime.Symbol object 's' with the
               information in the object info that is being typecasted
               as a java_cup.runtime.Symbol object. */
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
   
            /* Check if the line number in the input is greater or
               equal to zero. */
            if (s.left >= 0) {                
                /* Add to the end of the StringBuffer error message
                   the line number of the error in the input. */
                m.append(" in line "+(s.left+1));   
                /* Check if the column number in the input is greater
                   or equal to zero. */
                if (s.right >= 0)                    
                    /* Add to the end of the StringBuffer error message
                       the column number of the error in the input. */
                    m.append(", column "+(s.right+1));
            }

            m.append(" (" + s.toString() + ")");
        }
   
        /* Add to the end of the StringBuffer error message created in
           this method the message that was passed into this method. */
        m.append(" : "+message);
   
        /* Print the contents of the StringBuffer 'm', which contains
           an error message, out on a line. */
        System.err.println(m);
    }
   
    /* Change the method report_fatal_error so when it reports a fatal
       error it will display the line and column number of where the
       fatal error occurred in the input as well as the reason for the
       fatal error which is passed into the method in the object
       'message' and then exit.*/
    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
        System.exit(1);
    }
:};
   
   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
terminal String IF, INT, RETURN, VOID, WHILE, ELSE;
terminal String PLUS, MINUS, MULT, DIV;
terminal String LESS_THAN, LESS_THAN_EQUAL, GREATER_THAN, GREATER_THAN_EQUAL, EQUAL, NOT_EQUAL, ASSIGN;
terminal String SEMICOLON, COMMA;
terminal String LBRACKET, RBRACKET, LSQUARE_BRACKET, RSQUARE_BRACKET, LCURLY_BRACKET, RCURLY_BRACKET;
terminal String ID; 
terminal Integer NUM;

non terminal DecList program;
non terminal DecList declaration_list;
non terminal Dec declaration;
non terminal VarDec var_declaration;
non terminal FunctionDec fun_declaration;

non terminal VarDecList param_list, local_declarations;
non terminal VarDec param;
non terminal CompoundExp compound_stmt;

non terminal IfExp selection_stmt, selection_else_stmt, selection_noelse_stmt; 
non terminal WhileExp iteration_stmt; 
non terminal ReturnExp return_stmt; 
non terminal Var var;

non terminal Exp arithmetic_expression, simple_expression;
non terminal Integer operation, relop;
non terminal NameTy type_specifier;
non terminal Exp expression, factor, statement, expression_stmt;
non terminal CallExp call;
non terminal ExpList arg_list, statement_list;


/*If no other rules are satisfied, it is an error*/
precedence left error;

precedence left ASSIGN;
precedence nonassoc LESS_THAN, LESS_THAN_EQUAL, GREATER_THAN, GREATER_THAN_EQUAL, EQUAL, NOT_EQUAL;

/*Multiply before adding*/
precedence left PLUS, MINUS;
precedence left MULT, DIV;

precedence left IF;
precedence left ELSE;

/* ----------------------------Grammar Section-------------------- */
   
program ::= declaration_list:s1 
        {:  
            RESULT = s1;
        :}
        ;

declaration_list    ::= declaration:s1 declaration_list:s2 {: RESULT = new DecList(s1, s2); :}
                    | /*Empty*/ {: RESULT = new DecList(null, null); :}
                    ;

declaration ::= var_declaration:s1 {: RESULT = s1; :}
            |  fun_declaration:s1 {: RESULT = s1; :}
            ;

fun_declaration ::= type_specifier:s1 ID:s2 LBRACKET param_list:s3 RBRACKET compound_stmt:s4 
                {: RESULT = new FunctionDec(s1left, s1, s2, s3, s4); :}

                | type_specifier:s1 ID:s2 LBRACKET VOID RBRACKET compound_stmt:s3 
                {: RESULT = new FunctionDec(s1left, s1, s2, null, s3); :}

                | type_specifier:s1 ID:s2 LBRACKET error RBRACKET compound_stmt:s3 
                {: parser.report_error("Error starting on line " + s1left + ": invalid parameters", null);  :}
                ;

var_declaration ::= type_specifier:s1 ID:s2 SEMICOLON {: RESULT = new SimpleDec(s1left, s1, s2); :}
                | type_specifier:s1 ID:s2 LSQUARE_BRACKET NUM:s3 RSQUARE_BRACKET SEMICOLON 
                {: RESULT = new ArrayDec(s1left, s1, s2, new IntExp(s3left, s3)); :}
                | type_specifier:s1 ID:s2 LSQUARE_BRACKET error RSQUARE_BRACKET SEMICOLON
                {: parser.report_fatal_error("Fatal error on line " + s1left + ": invalid array length.", null); :}
                | error:i SEMICOLON {: parser.report_fatal_error("Fatal error on line " + ileft + ": invalid declaration.", null); :}
                ;

type_specifier  ::= INT:s1 {: RESULT = new NameTy(s1left, NameTy.INT); :}
                | VOID:s1 {: RESULT = new NameTy(s1left, NameTy.VOID); :}
                ;

param_list  ::= param:s1 COMMA param_list:s2 {: RESULT = new VarDecList(s1, s2); :}
            |  param:s1 {: RESULT = new VarDecList(s1, null); :}
            | /*Empty*/ {: RESULT = new VarDecList(null, null); :}
            ;

param   ::= type_specifier:s1 ID:s2 {: RESULT = new SimpleDec(s1left, s1, s2); :}
        |  type_specifier:s1 ID:s2 LSQUARE_BRACKET RSQUARE_BRACKET {: new ArrayDec(s1left, s1, s2, new IntExp(s1left, 0)); :}
        ;

compound_stmt   ::= LCURLY_BRACKET:i local_declarations:s1 statement_list:s2 RCURLY_BRACKET {: RESULT = new CompoundExp(ileft, s1, s2); :}
                | LCURLY_BRACKET:i error RCURLY_BRACKET {: parser.report_error("Error on line " + ileft + ": invalid block", null); :}
                ;

local_declarations  ::= var_declaration:s1 local_declarations:s2 {: RESULT = new VarDecList(s1, s2); :}
                    | /*Empty*/ {: RESULT = new VarDecList(null, null); :}
                    ;

statement_list  ::= statement:s1 statement_list:s2 {: RESULT = new ExpList(s1, s2);:}
                | /*Empty*/ {: RESULT = new ExpList(null, null); :}
                ;

statement   ::= expression_stmt:s1 {: RESULT = s1; :}
            | compound_stmt:s1 {: RESULT = s1; :}
            | selection_stmt:s1 {: RESULT = s1; :}
            | iteration_stmt:s1 {: RESULT = s1; :}
            | return_stmt:s1 {: RESULT = s1; :}
            ;

expression_stmt ::= expression:s1 SEMICOLON {: RESULT = s1; :}
                | error:i SEMICOLON 
                {: parser.report_error("Error on line " + ileft + ": invalid expression", null); RESULT = new NilExp(ileft); :}
                ;

selection_stmt  ::= selection_else_stmt:s1 {:RESULT = s1;:}
                |   selection_noelse_stmt:s1 {:RESULT = s1;:}
                ;

selection_else_stmt ::= IF:i LBRACKET expression:s1 RBRACKET statement:s2 ELSE statement:s3 {:RESULT = new IfExp(ileft, s1, s2, s3); :} ;
selection_noelse_stmt ::= IF:i LBRACKET expression:s1 RBRACKET statement:s2 {:RESULT = new IfExp(ileft, s1, s2, null);:} ;

iteration_stmt  ::= WHILE:i LBRACKET expression:s1 RBRACKET statement:s2 {:RESULT = new WhileExp(ileft, s1, s2); :}
                ;

return_stmt ::= RETURN:i SEMICOLON {:RESULT=new ReturnExp(ileft, new NilExp(ileft)); :}
            | RETURN:i expression:s1 SEMICOLON {: RESULT = new ReturnExp(ileft, s1); :}
            | RETURN:i error SEMICOLON 
            {: parser.report_error("Error on line " + ileft + ": invalid return", null); RESULT = new ReturnExp(ileft, new NilExp(ileft)); :} 
            ;

expression  ::= var:s1 ASSIGN expression:s2 {: RESULT = new AssignExp(s1left, s1, s2); :}
            | simple_expression:s1 {: RESULT = s1; :} 
            ;

var ::= ID:s1 {:RESULT = new SimpleVar(s1left, s1); :}
    | ID:s1 LSQUARE_BRACKET expression:s2 RSQUARE_BRACKET {: RESULT = new IndexVar(s1left, s1, s2); :}
    ;

simple_expression   ::= arithmetic_expression:s1 relop:s2 arithmetic_expression:s3 {:RESULT = new OpExp(s1left, s1, s2, s3); :}
                    |  arithmetic_expression:s1 {: RESULT = s1; :}
                    |  arithmetic_expression:s1 error arithmetic_expression:s2 {: parser.report_error("Error on line " + s1left + ": Invalid comparison operator", null); :}
                    ;

relop   ::= LESS_THAN_EQUAL {:RESULT=OpExp.LE; :}
        | LESS_THAN {:RESULT=OpExp.LT; :}
        | GREATER_THAN {:RESULT=OpExp.GT; :}
        | GREATER_THAN_EQUAL {:RESULT=OpExp.GE; :}
        | EQUAL {: RESULT=OpExp.EQ;:}
        | NOT_EQUAL {:RESULT=OpExp.NE; :}
        ;

/*Minimized from additive-expression*/
arithmetic_expression   ::= arithmetic_expression:s1 operation:s2 arithmetic_expression:s3 {: RESULT = new OpExp(s1left, s1, s2, s3); :}
                        | factor:s1 {: RESULT = s1; :}
                        ;

operation   ::= PLUS {:RESULT=OpExp.PLUS; :}
            | MINUS {:RESULT=OpExp.MINUS; :}
            | MULT {:RESULT=OpExp.MULT;  :}
            | DIV {:RESULT=OpExp.DIV;  :}
            ;

factor  ::= LBRACKET expression:s1 RBRACKET {: RESULT = s1; :}
        | var:s1 {: RESULT = new VarExp(s1left, s1); :}
        | call:s1 {: RESULT = s1; :}
        | NUM:s1 {: RESULT = new IntExp(s1left, s1); :}
        ;

call    ::= ID:s1 LBRACKET arg_list:s2 RBRACKET {: RESULT = new CallExp(s1left, s1, s2); :}
        | ID:s1 LBRACKET RBRACKET {: RESULT = new CallExp(s1left, s1, null); :}
        | ID:s1 LBRACKET error RBRACKET {: parser.report_error("Error on line " + s1left + ": invalid call parameters", null); :}
        ;

arg_list    ::= expression:s1 COMMA arg_list:s2 {: RESULT = new ExpList(s1, s2); :}
            | expression:s1 {: RESULT = new ExpList(s1, null); :}
            | /*Empty*/ {: RESULT = new ExpList(null, null); :}
            ;

